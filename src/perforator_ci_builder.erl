%% @doc Projects builder. 
%% The builder stores (non persistent) build requests in its queue
%% and executes (FIFO) only one at a time in spawned process.
%% If builder (not spawned process) dies, all projects resend build requests.
%%
%% There could be more than one builder instance, but it should run on
%% different node %% (and preferable on different host),
%% but there is no auto node discovery. So either put in kernel app
%% `sync_nodes_optional` or do manually net_adm:ping/1.

%% @author Martynas <martynasp@gmail.com>

-module(perforator_ci_builder).

-behaviour(gen_server).

-include("perforator_ci.hrl").

%% API
-export([
    start_link/0,
    build/2,
    get_queue_size/1,
    get_builders/0,

    run_build/1 % used for mocking
]).

%% gen_server callbacks
-export([
    init/1,
    terminate/2,
    handle_call/3,
    handle_cast/2,
    handle_info/2,
    code_change/3
]).

-type queue_item() :: {ProjectPid :: pid(), #project{}, #project_build{}}.

-record(state, {
    build_queue=[] :: [queue_item()],
    worker :: pid() | undefined % pid of process responsible for handling build
    % undefined means that currently there is no job running
}).

-ifdef(TEST).
-compile(export_all).
-endif.

-define(SERV_NAME, {global, {perforator_ci_builder, node()}}).
-define(BUILD_REQUEST_TIMEOUT, 15000). % @todo put it into config

%% ============================================================================
%% API
%% ============================================================================

%% @doc Starts builder.
%% Builder name is global and a form of {perforator_ci_builder, node()}.
-spec start_link() -> {ok, term()}.
start_link() ->
    gen_server:start_link(?SERV_NAME, ?MODULE, [], []).

%% @doc Adds build request (which will be eventually handled) to builder queue.
-spec build(#project{}, #project_build{}) -> ok.
build(Project, Build) ->
    % Pick randomly a builder
    Builder = pick_builder(),
    true = link(global:whereis_name(Builder)), % link for request,
    % all persistent part is built at perforator_ci_project side
    gen_server:call({global, Builder},
        {build, Project, Build}, ?BUILD_REQUEST_TIMEOUT).

%% @doc Returns builder queue size.
-spec get_queue_size(node()) -> integer().
get_queue_size(Node) ->
    length(gen_server:call({global, {perforator_ci_builder, Node}}, get_queue)).

%% @doc Used only for testing.
get_queue() ->
    gen_server:call(?SERV_NAME, get_queue).

%% @doc Used only for testing.
get_worker() ->
    gen_server:call(?SERV_NAME, get_worker).

%% =============================================================================
%% gen_server callbacks
%% =============================================================================

init([]) ->
    process_flag(trap_exit, true), % we don't want to let a process die like
    % the rest of the world (perforator_ci_project that links to
    % perforator_ci_builder before doing request).

    {ok, #state{}}.

%% for testing
handle_call(get_queue, _, #state{build_queue=Q}=S) ->
    {reply, Q, S};

%% for testing
handle_call(get_worker, _, #state{worker=P}=S) ->
    {reply, P, S};

% Ask to build
handle_call({build, Project, Build}, {Pid, _},
        #state{build_queue=Q}=State) ->
    % Inserts request to the queue (safe for duplicates):
    Q1 = enqueue({Pid, Project, Build}, Q),

    ok = perforator_ci_pubsub:broadcast(perforator_ci_builder,
        {queue_size, {node(), length(Q1)}}),

    gen_server:cast(self(), ping), % ping!

    {reply, ok, State#state{build_queue=Q1}};

handle_call(_, _, State) ->
    {reply, ok, State}.

% empty queue, do nothing
handle_cast(ping, #state{build_queue=[]}) ->
    {noreply, #state{}};

% busy, do nothing
handle_cast(ping, #state{worker=P}=S) when is_pid(P) ->
    {noreply, S};

% build!
handle_cast(ping, #state{build_queue=[Item|_]}=S) ->
    Pid = spawn_link(
        fun() ->
            {Pid, _, #project_build{id=BuildID}} = Item,
            {Success, Result} =
                try
                    {true, ?MODULE:run_build(Item)}
                catch
                    throw:R ->
                        {false, R}
                end,

            % inform
            ok = perforator_ci_project:build_finished(Pid, BuildID, Result,
                Success),
            % exit
            exit('$work_is_done')
        end
    ),

    {noreply, S#state{worker=Pid}};

handle_cast(_, State) ->
    {noreply, State}.

% worker done!
handle_info({'EXIT', Pid, '$work_is_done'},
        #state{worker=Pid, build_queue=[_|T]}) ->
    ok = perforator_ci_pubsub:broadcast(perforator_ci_builder,
        {queue_size, {node(), length(T)}}),
    gen_server:cast(self(), ping),

    {noreply, #state{build_queue=T}};

% worker died ;-( restart it
handle_info({'EXIT', Pid, _}, #state{worker=Pid, build_queue=Q}) ->
    gen_server:cast(self(), ping),

    {noreply, #state{build_queue=Q}};

% project died, delete all its queue items
handle_info({'EXIT', Pid, _}, #state{build_queue=Q, worker=P}) ->
    % kill current worker if its handling died project build:
    P1 = case Q of
        [{Pid, _, _}|_] ->
            if
                is_pid(P) ->
                    erlang:exit(P, '$project_died'),
                    undefined;
                true ->
                    undefined
            end;
        _ -> P
    end,

    % filter out queue items
    Q1 = [Item || {Pid0, _, _}=Item <- Q, Pid0 =/= Pid],

    ok = perforator_ci_pubsub:broadcast(perforator_ci_builder,
        {queue_size, {node(), length(Q1)}}),

    {noreply, #state{build_queue=Q1, worker=P1}};

handle_info(_, State) ->
    {noreply, State}.

code_change(_, State, _) ->
    {ok, State}.

terminate(_, _) ->
    ok.

%% =============================================================================
%% Helpers
%% =============================================================================

%% @doc Picks randomly a builder.
pick_builder() ->
    Builders = [B ||
        {perforator_ci_builder, _X}=B <- global:registered_names()],

    lists:nth(random:uniform(length(Builders)), Builders).

%% @doc Returns nodes that run builders.
get_builders() ->
    [B || {perforator_ci_builder, B} <- global:registered_names()].

%% @doc Adds item to an queue if item with given build id doesn't exist.
-spec enqueue(queue_item(), [queue_item()]) -> [queue_item()].
enqueue({_, _, #project_build{id=BuildID}}=Item, Q) ->
    NotExists = lists:all(
        fun
            ({_, _, #project_build{id=BID}}) when BID =:= BuildID -> false;
            (_) -> true
        end,
        Q
    ),
    
    if
        NotExists -> Q ++ [Item];
        true -> Q
    end.

%% @doc Run a job.
%% @throws perf_test_not_found.
run_build({_Pid, #project{id=ProjectID, repo_url=RepoUrl,
        repo_backend=Mod, build_instructions=Instructions},
        #project_build{commit_id=CommitID}}) ->
    % Check if repo exist. If not, clone it
    RepoDir = repo_path(ProjectID),
    case filelib:is_dir(RepoDir) of
        true -> ok;
        false ->
            ok = Mod:clone(RepoUrl, RepoDir)
    end,

    % Fetch and checkout
    ok = Mod:checkout(RepoDir, CommitID),

    % Try to exec project build instructions:
    lists:foreach(
        fun (C) ->
            try
                perforator_ci_utils:sh(C, [{cd, RepoDir}])
            catch
                throw:{exec_error, {_, _, Reason}} ->
                    throw({C, Reason});
                error:badarg ->
                    lager:error("Internal error: ~p~n",
                        [erlang:get_stacktrace()]),
                    throw({internal_error, C})
            end
        end,
        Instructions
    ),

    % Find and return `perforator` statistics.
    case perforator_ci_results:read(RepoDir) of
        [] -> % @todo Clean:
            throw(perf_test_not_found);
        Res -> Res
    end.

%% @doc Returns builder repo_path.
%% @todo DRY
repo_path(ProjectID) ->
    filename:join(
        perforator_ci_utils:get_env(perforator_ci,
            builder_repos_path, ?BUILDER_REPOS_DIR),
        binary_to_list(ProjectID)
    ).
